%option case-insensitive

h             [0-9a-f]
nonascii      [\200-\377]
unicode       \\{h}{1,6}[ \t\r\n\f]?
escape        {unicode}|\\[ -~\200-\377]
nmchar        [a-z0-9-]|{nonascii}|{escape}
em1           \*[^\*]*\*
strong1       \*\*[^\*]*\*\*
strongem1     \*\*\*[^\*]*\*\*\*
em2           _[^_]*_
strong2       __[^_]*__
strongem2     ___[^_]*___
header1       ^#{1,6}.*
header2       .+\n[=-]+

protocol     (http|ftp)s?\:\/\/
subdomain    {nmchar}\.
urlpath      [\/]|{nmchar}+
protourl     {protocol}[^ ]+
optprotourl  {protocol}?{subdomain}*{nmchar}+\.{nmchar}+{urlpath}*?
href         \[.+\]\(.+\)

em           {em1}|{em2}
strong       {strong1}|{strong2}
strongem     {strongem1}|{strongem2}

url          {protourl}|{optprotourl}

w            [ \t\r\n\f]*
nl           \n|\r\n|\r|\f

%%

{em}                            {markdownConsume(yytext, MARKDOWNEM);}
{strong}                        {markdownConsume(yytext, MARKDOWNSTRONG);}
{strongem}                      {markdownConsume(yytext, MARKDOWNSTRONGEM);}
{header1}                       {markdownConsume(yytext, MARKDOWNHEADER);}
{header2}                       {markdownConsume(yytext, MARKDOWNMULTILINEHEADER);}
{url}                           {markdownConsume(yytext, MARKDOWNURL);}
{href}                          {markdownConsume(yytext, MARKDOWNHREF);}

[ ]{2,}                         {markdownConsume(yytext, MARKDOWNPARAGRAPH);}
[\n]{2,}                        {markdownConsume(yytext, MARKDOWNPARAGRAPH);}
[\n]                            {markdownConsume(yytext, MARKDOWNNEWLINE);}
[ \t\f]                         {markdownConsume(yytext, MARKDOWNUNKNOWN);}
.                               {markdownConsume(yytext, MARKDOWNUNKNOWN);}

%%

int markdownwrap(void){return 1;}

